#compdef wdctl
# ------------------------------------------------------------------------------
# Copyright (c) 2025 Github zsh-users - https://github.com/zsh-users
#
# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:
#
# The above copyright notice and this permission notice shall be included
# in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS
# OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL
# THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR
# OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
# ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
# OTHER DEALINGS IN THE SOFTWARE.
# ------------------------------------------------------------------------------
# Description
# -----------
#
#  Completion script for wdctl v2.41 (https://github.com/util-linux/util-linux)
#
# ------------------------------------------------------------------------------
# Authors
# -------
#
#  * Shohei Yoshida (https://github.com/syohex) <syohex@gmail.com>
#
# ------------------------------------------------------------------------------

(( $+functions[_wdctl_output_columns] )) ||
_wdctl_output_columns() {
  local -a columns=(
    "FLAG[flag name]"
    "DESCRIPTION[flag description]"
    "STATUS[flag status]"
    "BOOT-STATUS[flag boot status]"
    "DEVICE[watchdog device name]"
  )

  _values -s ',' column $columns
}

_arguments -s \
  '(- *)'{-h,--help}'[display this help]' \
  '(- *)'{-V,--version}'[display version]' \
  '(-f --flags)'{-f,--flags}'[print selected flags only]:flags' \
  '(-F --noflags)'{-F,--noflags}'[do not print information about flags]' \
  '(-I --noident)'{-I,--noident}'[do not print watchdog identity information]' \
  '(-n --noheadings)'{-n,--noheadings}'[do not print headings for flags table]' \
  '(-O --oneline)'{-O,--oneline}'[print all information on one line]' \
  '(-o --output)'{-o,--output}'[output columns]:columns:_wdctl_output_columns' \
  '(-p --setpretimeout)'{-p,--setpretimeout}'[set watchdog pre-timeout]:seconds' \
  '(-g --setpregovernor)'{-g,--setpregovernor}'[set pre-timeout governor]:name' \
  '(-r --raw)'{-r,--raw}'[use raw output format for flags table]' \
  '(-T --notimeouts)'{-T,--notimeouts}'[do not print watchdog timeouts]' \
  '(-s --settimeout)'{-s,--settimeout}'[set watchdog timeout]:seconds' \
  '(-x --flags-only)'{-x,--flags-only}'[print only flags table]'

# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
